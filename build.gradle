buildscript {
    repositories {
        jcenter {
            url "https://jcenter.bintray.com"
        }
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.3.0'
        classpath 'nu.studer:gradle-plugindev-plugin:1.0.4'
        classpath 'org.codehaus.groovy:groovy-backports-compat23:2.4.6'
        classpath 'net.researchgate:gradle-release:2.6.0'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'nu.studer.plugindev'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'net.researchgate.release'

group='net.researchgate'

dependencies {
    testCompile("org.spockframework:spock-core:$spockVersion") { exclude group: 'org.codehaus.groovy' }
    testCompile "junit:junit:$junitVersion"
    testCompile "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
    testCompile "cglib:cglib-nodep:$cglibVersion"
}

plugindev {
    pluginImplementationClass 'net.researchgate.release.ReleasePlugin'
    pluginDescription 'gradle-release is a plugin for providing a Maven-like release process to project using Gradle.'
    pluginLicenses 'MIT'
    pluginTags 'gradle', 'plugin', 'release'
    authorId 'hillkorn'
    authorName 'Dennis Schumann'
    authorEmail 'dennis.schumann@researchgate.net'
    projectUrl 'https://github.com/researchgate/gradle-release'
    projectInceptionYear '2011'
    done() // do not omit this
}

release {
    git {
        requireBranch = '(master|\\d+\\.\\d+)'
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : ''
    key = project.hasProperty('bintrayApiKey') ? bintrayApiKey : ''
    pkg {
        repo = 'gradle-plugins'
        userOrg = 'researchgate'
        version {
            gpg {
                sign = false
            }
            mavenCentralSync {
                sync = false
                user = project.hasProperty('sonatypeUser') ? sonatypeUser : ''
                password = project.hasProperty('sonatypePassword') ? sonatypePassword : ''
            }
        }
    }
    publish = false
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local' //The Artifactory repository key to publish to
            username = project.hasProperty('bintrayUser') ? bintrayUser : ''
            password = project.hasProperty('bintrayApiKey') ? bintrayApiKey : ''
        }
        defaults {
            publications 'plugin' // That is how it is named in plugindev plugin
            properties = ['bintray.repo': 'gradle-plugins', 'bintray.package': 'gradle-release', 'bintray.version': version.toString()]
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
}

afterReleaseBuild.dependsOn bintrayUpload

wrapper.gradleVersion = '1.12'

updateVersion.doFirst {
    def file = file('README.md')
    def content = file.text
    def versionPattern = /\d+(?:\.\d+)+/
    content = content.replaceAll("id 'net.researchgate.release' version '${versionPattern}'",
            "id 'net.researchgate.release' version '${version}'")
    content = content.replaceAll("net.researchgate:gradle-release:${versionPattern}",
            "net.researchgate:gradle-release:${version}")
    file.text = content
}
