buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.3.0'
        classpath 'nu.studer:gradle-plugindev-plugin:1.0.4'
        classpath 'org.codehaus.groovy:groovy-backports-compat23:2.4.6'
        classpath 'net.researchgate:gradle-release:2.6.0'
    }
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'nu.studer.plugindev'
apply plugin: 'net.researchgate.release'

group='net.researchgate'

dependencies {
    testCompile("org.spockframework:spock-core:$spockVersion") { exclude group: 'org.codehaus.groovy' }
    testCompile "junit:junit:$junitVersion"
    testCompile "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
    testCompile "cglib:cglib-nodep:$cglibVersion"
}

plugindev {
    pluginImplementationClass 'net.researchgate.release.ReleasePlugin'
    pluginDescription 'gradle-release is a plugin for providing a Maven-like release process to project using Gradle.'
    pluginLicenses 'MIT'
    pluginTags 'gradle', 'plugin', 'release'
    authorId 'RG'
    authorEmail 'dennis.schumann@researchgate.net'
    authorName 'ResearchGate'
    projectUrl 'https://github.com/researchgate/gradle-release'
    projectInceptionYear '2011'
    done() // do not omit this
}

gradlePlugin {
    plugins {
        releasePlugin {
            id = 'net.researchgate.release'
            implementationClass = 'net.researchgate.release.ReleasePlugin'
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : ''
    key = project.hasProperty('bintrayApiKey') ? bintrayApiKey : ''
    pkg {
        repo = 'gradle-plugins'
        userOrg = 'researchgate'
        version {
            gpg {
                sign = false
            }
            mavenCentralSync {
                sync = false
                user = project.hasProperty('sonatypeUser') ? sonatypeUser : ''
                password = project.hasProperty('sonatypePassword') ? sonatypePassword : ''
            }
        }
    }
    publish = false
}

afterReleaseBuild.dependsOn bintrayUpload

wrapper.gradleVersion = '3.5.1'
