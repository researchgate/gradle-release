apply plugin: 'groovy'
apply plugin: 'maven'
apply from: 'installation/apply-local.groovy'

configurations {
	releaseArtifacts {}
	installers {}
	deployerJars {}
}

repositories {
	mavenCentral()
	mavenRepo url: 'http://m2repo.spockframework.org/snapshots'
}

dependencies {
	compile gradleApi(),
			'org.ajoberstar:gradle-git:0.5.0'
	groovy localGroovy()
	testCompile( 'org.spockframework:spock-core:0.5-groovy-1.8' ) { exclude group: 'org.codehaus.groovy' }
	testCompile 'junit:junit:4.10'
	deployerJars 'org.apache.maven.wagon:wagon-ftp:2.2'
}

// This updates the apply script with the new version.
updateVersion << {
	def applyFile = new File("installation", "apply.groovy")
	def oldVersion = project.getProperty("release.oldVersion")
	def newVersion = project.getProperty("release.newVersion")
	ant.replace(file: applyFile, token: "${group}:${name}:${oldVersion}", value: "${group}:${name}:${newVersion}")
}

artifacts {
	releaseArtifacts jar
	installers file('installation/apply.groovy')
}

uploadReleaseArtifacts {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars
			repository(url: releaseRepoUrl) { authentication(userName: releaseRepoUser, password: releaseRepoPass) }
		}
	}
}

uploadInstallers {
	repositories {
		add(new org.apache.ivy.plugins.resolver.SFTPResolver()) {
			host = releaseRepoHost
			addArtifactPattern "${releaseRepoBase}/[module]/[revision]/[artifact].[ext]"
		}
	}
}

createReleaseTag.dependsOn uploadReleaseArtifacts
createReleaseTag.dependsOn uploadInstallers